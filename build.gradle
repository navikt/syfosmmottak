buildscript {
    ext.artemis_version = '2.6.2'
    ext.coroutines_version = '0.23.4'
    ext.fellesformat_version = '1.0'
    ext.ibm_mq_version = '9.1.0.0'
    ext.javax_activation_version = '1.1.1'
    ext.jackson_version = '2.9.7'
    ext.jaxb_api_version = '2.3.1'
    ext.jaxb_version = '2.3.0.1'
    ext.jedis_version = '2.9.0'
    ext.kafka_version = '2.0.0'
    ext.kafka_embedded_version = '1.0.0'
    ext.kith_hodemelding_version = '1.1'
    ext.kith_apprec_version = '1.1'
    ext.kluent_version = '1.39'
    ext.ktor_version = '1.1.1'
    ext.logback_version = '1.2.3'
    ext.logstash_encoder_version = '5.1'
    ext.prometheus_version = '0.5.0'
    ext.spek_version = '2.0.0-rc.1'
    ext.sykmelding_version = "1.1-SNAPSHOT"
    ext.subscription_version = "1.0.5"
    ext.cxf_version = '3.2.5'
    ext.jaxws_api_version = "2.2.1" // TODO: 2.3.0
    ext.commons_text_version = "1.4"
    ext.javax_annotation_api_version = "1.3.2"

    ext.jaxb2SourceDir = file("$buildDir/generated-sources/jaxb2")
}

plugins {
    id 'no.nils.wsdl2java' version '0.10'
    id 'org.jmailen.kotlinter' version '1.14.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id("com.diffplug.gradle.spotless") version "3.14.0"
    id("com.github.johnrengelman.shadow") version "4.0.3"
}

group 'no.nav.syfo'
version '1.0.21'

jar.manifest.attributes("Main-Class" : "no.nav.syfo.BootstrapKt")

shadowJar {
    mergeServiceFiles("META-INF/cxf")
}

configurations {
    navWsdl {
        transitive = false
    }
}

task copyWsdlFromArtifacts(type: Copy) {
    includeEmptyDirs = false
    from {
        configurations.navWsdl.asFileTree.forEach { artifact ->
            from zipTree(artifact)
        }
    }
    into "$buildDir/schema/"
    include '**/*.xsd', '**/*.wsdl'
}

wsdl2java {
    dependsOn copyWsdlFromArtifacts
    wsdlDir = file("$buildDir/schema")
    wsdlsToGenerate = [
            ["-xjc", "-b", "$projectDir/src/main/xjb/binding.xml", "$buildDir/schema/subscription.wsdl"],
    ]
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url = "http://packages.confluent.io/maven/" }
    maven { url 'https://repo.adeo.no/repository/maven-releases/' }
    maven { url 'https://repo.adeo.no/repository/maven-snapshots/' }
    maven { url "https://dl.bintray.com/spekframework/spek-dev" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        showStandardStreams = true
    }
}



compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task printVersion() {
    doLast {
        println(version)
    }
}

compileKotlin {
    dependsOn wsdl2java
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "io.prometheus:simpleclient_common:$prometheus_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    implementation "com.ibm.mq:com.ibm.mq.allclient:$ibm_mq_version"
    implementation "org.apache.kafka:kafka_2.12:$kafka_version"

    implementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    implementation "no.nav.helse.xml:sm2013:$sykmelding_version"
    implementation "no.nav.syfo.tjenester:fellesformat:$fellesformat_version"
    implementation "no.nav.syfo.tjenester:kith-hodemelding:$kith_hodemelding_version"
    implementation "no.nav.syfo.tjenester:kith-apprec:$kith_apprec_version"
    
    navWsdl "no.nav.tjenester:subscription-nav-emottak-eletter-web:$subscription_version@zip"
    
    implementation "org.apache.commons:commons-text:$commons_text_version"

    implementation "redis.clients:jedis:$jedis_version"

    implementation "javax.xml.bind:jaxb-api:$jaxb_api_version"
    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxb_version"
    implementation "javax.activation:activation:$javax_activation_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-client-auth-basic:$ktor_version"
    implementation "io.ktor:ktor-client-jackson:$ktor_version"

    // Communication libraries
    implementation "org.apache.cxf:cxf-rt-frontend-jaxws:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-features-logging:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-transports-http:$cxf_version"
    implementation "org.apache.cxf:cxf-rt-ws-security:$cxf_version"
    implementation "javax.xml.ws:jaxws-api:$jaxws_api_version"

    testImplementation "org.amshove.kluent:kluent:$kluent_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "no.nav:kafka-embedded-env:$kafka_embedded_version"

    testRuntimeOnly "org.spekframework.spek2:spek-runtime-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    testImplementation "org.apache.activemq:artemis-server:$artemis_version"
    testImplementation "org.apache.activemq:artemis-jms-client:$artemis_version"

    wsdl2java "javax.xml.bind:jaxb-api:$jaxb_api_version"
    wsdl2java "javax.xml.ws:jaxws-api:$jaxws_api_version"
    wsdl2java "javax.activation:activation:$javax_activation_version"
    wsdl2java "javax.annotation:javax.annotation-api:$javax_annotation_api_version"
}
